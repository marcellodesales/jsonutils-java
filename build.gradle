import org.gradle.api.XmlProvider
import org.gradle.api.artifacts.maven.MavenDeployment

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.github.marcellodesales'
archivesBaseName = "json-utils"
version = '1.0.0'
ext.packaging = 'jar'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

def isDevBuild
def isCiBuild
def isReleaseBuild

def sonatypeRepositoryUrl

//set build variables based on build type (release, continuous integration, development)
if(hasProperty("release")) {
    isReleaseBuild = true
    sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"

} else if (hasProperty("ci")) {
    isCiBuild = true
    version += "-SNAPSHOT"
    sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"

} else {
    isDevBuild = true
}

repositories {
    mavenCentral()
}

//********* artifact signing *********
if(isReleaseBuild) {
    signing {
        sign configurations.archives
    }
} else {
    task signArchives {
        // do nothing
    }
}

uploadArchives {
  repositories {

    if (isDevBuild) {
      mavenLocal()

    } else {

    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'JSON Utils'
        packaging 'jar'
        // optionally artifactId can be defined here 
        description 'Set of utilities around JSON utils'
        url 'http://github.com/marcellodesales/jsonutils-java'

        scm {
          url 'scm:git@github.com:marcellodesales/jsonutils-java.git'
          connection 'scm:git@github.com:marcellodesales/jsonutils-java.git'
          developerConnection 'scm:git@github.com:marcellodesales/jsonutils-java.git'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'marcellodesales'
            name 'Marcello de Sales'
            email 'marcello.desales@gmail.com'
          }
          }
        }
      }
    }
  }
}

